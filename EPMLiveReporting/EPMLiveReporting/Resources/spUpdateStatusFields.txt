CREATE PROCEDURE spUpdateStatusFields

@listname varchar(255),
@yellow int = 0,
@red int = 30

AS
BEGIN

declare @listtable as varchar(260)

if @listname = 'Resources'
begin
      set @listtable = 'LstResourcepool'
end
else
begin
      set @listtable = 'LST' + @listname
end

declare @cols as bit
set @cols = 1
declare @sql as varchar(MAX)

if exists (select column_name FROM INFORMATION_SCHEMA.COLUMNS where table_name = @listtable and column_name = 'DueDate')
begin
      ----------------------Days Overdue------------
      if exists (select column_name FROM INFORMATION_SCHEMA.COLUMNS where table_name = @listtable and column_name = 'DaysOverdue')
      begin
            
            set @sql = 'update ' + @listtable + ' set daysoverdue = case when duedate > GETDATE() then 0 else DATEDIFF(d, duedate, GETDATE()) end'
            
            exec (@sql)
            
      end
      
      
      ----------------------Due------------
      if exists (select column_name FROM INFORMATION_SCHEMA.COLUMNS where table_name = @listtable and column_name = 'Due') AND  exists (select column_name FROM INFORMATION_SCHEMA.COLUMNS where table_name = @listtable and column_name = 'Status')
      begin
            
            set @sql = 'update ' + @listtable + ' set due = 
                              case 
                              when status = ''Completed'' then ''Completed'' 
                              when duedate is null then ''No Due Date''
                              when DATEDIFF(d, duedate, GETDATE()) > 0 then ''(1) Overdue''
                              when DATEDIFF(d, duedate, GETDATE()) = 0 then ''(2) Due Today''
                              when DATEDIFF(d, duedate, GETDATE()) = -1 then ''(3) Due Tomorrow''
                              when DATEPART(yyyy, duedate) = DATEPART(yyyy, GETDATE()) AND DATEPART(ww, duedate) = DATEPART(ww, GETDATE()) then ''(4) Due This Week''
                              when DATEDIFF(d, DATEADD (d , (DATEPART(dw,GETDATE()) - 1) * -1 , GETDATE()), duedate ) < 14 then ''(5) Due Next Week''
                              when DATEPART(yyyy, duedate) = DATEPART(yyyy, GETDATE()) AND DATEPART(m, duedate) = DATEPART(m, GETDATE()) then ''(6) Due This Month''
                              else ''(7) Future''
                              end'
            
            exec (@sql)
            
      end
      
      ----------------------Schedule Status------------
      if exists (select column_name FROM INFORMATION_SCHEMA.COLUMNS where table_name = @listtable and column_name = 'ScheduleStatus')
      begin
            
            set @sql = 'update ' + @listtable + ' set ScheduleStatus =        
                              case 
                              when duedate IS NULL then ''GREEN.GIF''
                              when DATEDIFF(d, duedate, GETDATE()) > ' + CONVERT(varchar(5), @red) + ' then ''RED.GIF''
                              when DATEDIFF(d, duedate, GETDATE()) > ' + CONVERT(varchar(5), @yellow) + ' then ''YELLOW.GIF''                      
                              else ''GREEN.GIF''
                              end'
            exec (@sql)

      end
end


END
